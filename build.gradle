apply plugin: "java"
apply plugin: "idea"

idea {
    project {
        ipr {
            withXml {
                def project = it.asNode()
                def vcsDirectoryMappings = project.component.find { it.@name == 'VcsDirectoryMappings' };
                vcsDirectoryMappings.children().clear()
                vcsDirectoryMappings.appendNode('mapping', ['directory': '$PROJECT_DIR$', 'vcs': 'Git'])
            }
        }
    }
}

dependencies {
    testCompile 'junit:junit:4.11'
    testCompile 'com.google.guava:guava:14.0-rc2'
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
    }
}

apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.signed.jvm-protocol-handler'
version = '0.0.2-SNAPSHOT'


def environments = [
        'local': [
                repositoryLogin: deploymentNexusLocalLogin,
                repositoryPassword: deploymentNexusLocalPassword,
                'urls': [
                        'snapshot': 'http://localhost:8081/nexus/content/repositories/snapshots',
                        'release' : 'http://localhost:8081/nexus/content/repositories/releases'

                ]
        ],
        'sonatype': [
                repositoryLogin: deploymentSonatypeLogin,
                repositoryPassword: deploymentSonatypePassword,
                'urls': [
                        'snapshot': 'https://oss.sonatype.org/content/repositories/snapshots',
                        'release' : 'https://oss.sonatype.org/service/local/staging/deploy/maven2'

                ]
        ]
]


def intent = hasProperty('release') ? 'release' : 'snapshot'
def target = hasProperty('sonatype') ? 'sonatype' : 'local'
println "intent: $intent target: $target url: ${environments.get(target).urls.get(intent)}"

def environment = environments.get(target)
if (project.hasProperty('release')) {
    project.version -= '-SNAPSHOT'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

//http://jedicoder.blogspot.de/2011/11/automated-gradle-project-deployment-to.html
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: environment.urls.get(intent)) {
                authentication(userName: environment.repositoryLogin, password: environment.repositoryPassword)
            }

            pom.project {
                name 'jvm-protocol-handler'
                packaging 'jar'
                inceptionYear '2012'
                description 'jvm-protocol-handler enables the use of jvm://key urls to resolve objects'
                url 'https://github.com/signed/jvm-protocol-handler'

                scm {
                    url 'scm:git@github.com:signed/jvm-protocol-handler.git'
                    connection 'scm:git://github.com/signed/jvm-protocol-handler.git'
                    developerConnection 'scm:git@github.com:signed/jvm-protocol-handler.git'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://raw.github.com/signed/jvm-protocol-handler/master/license'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'thomasheilbronner'
                        name 'Thomas Heilbronner'
                    }
                }
            }
        }
    }
}
